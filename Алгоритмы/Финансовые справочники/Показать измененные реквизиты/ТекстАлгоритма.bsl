// >> 1БИТ, Скоков, 09.12.2020, Задача MDM-54
//
// Описание алгоритма:
// 		Алгоритм просмотра измененных реквизитов универсальных справочников.
// 		Выводит различия между измененными реквизитами заявки и оригиналом.
// 		Вызывается при выборе произвольно команды в форме заявки на изменение:
// 		Обработка заявок -> Показать измененные реквизиты
//
// Настройки алгоритма:
// 		Наименование			Показать измененные реквизиты
// 		Область применения		Произвольные команды
// 		Выполнять на клиенте	Да
//
// Для работы алгоритма, необходимо на вкладке параметры добавить вспомогательные алгоритмы:
// 		"Получить структуру реквизитов" (имя параметра ПолучитьРеквизиты)
//
// Параметры передаваемые в алгоритм:
// 		ОбъектОбработки			ДанныеФормыСтруктура
// 		Параметры.Форма			ФормаКлиентскогоПриложения
// 		Параметры.Алгоритм		СправочникСсылка.нсиАлгоритмыОбработкиДанныхЗаявок

#Область Алгоритм_просмотра_измененных_реквизитов

ИзменяемыеОбъекты = ОбъектОбработки.ИзменяемыеОбъекты;

Если ИзменяемыеОбъекты.Количество() = 0 ИЛИ НЕ ЗначениеЗаполнено(ИзменяемыеОбъекты[0].Объект) Тогда
	ТекстСообщения = НСтр("ru = 'Не найден изменяемый объект!
			|Сравнивать можно только заявки на изменение'");
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
Иначе
	ПараметрыТекущегоАлгоритма = нсиАлгоритмыОбработкиДанныхЗаявокСервер.ПолучитьПараметрыАлгоритма(Параметры.Алгоритм);
	СтруктураРеквизитов = нсиАлгоритмыОбработкиДанныхЗаявокСервер.ВыполнитьАлгоритм(
			ПараметрыТекущегоАлгоритма.ПолучитьРеквизиты, ОбъектОбработки.ВидЗаявки);
	
	ОбъектОригинал = ИзменяемыеОбъекты[0].Объект;
	ВидСправочника = нсиОбщегоНазначения.ПолучитьЗначениеРеквизита(ОбъектОригинал, "Владелец");
	МетаданныеОбъекта = нсиУниверсальноеХранилище.ПолучитьМетаданные(ВидСправочника);
	МетаданныеОбъекта.Вставить("ТабличныеЧасти", Новый Структура());
	
	ДанныеОбъектаОригинального = нсиУниверсальноеХранилище.ПолучитьОбъект(МетаданныеОбъекта, ОбъектОригинал);
	ДанныеОбъектаИзмененного = Параметры.Форма;
	
	РезультатСравнения = Новый Структура;
	РезультатСравнения.Вставить("ЕстьИзменения", Ложь);
	РезультатСравнения.Вставить("ЕстьИзмененияВЗаполненныхРеквизитах", Ложь);
	РезультатСравнения.Вставить("СопоставлениеРеквизитов", Новый Массив);
	
	Для Каждого ПроверяемыйРеквизит Из СтруктураРеквизитов Цикл
		ИмяРеквизита = ПроверяемыйРеквизит.Ключ;
		
		ЗначениеРеквизитаОригинального = "";
		Если НЕ ДанныеОбъектаОригинального.Свойство(ИмяРеквизита, ЗначениеРеквизитаОригинального) Тогда
			Продолжить;
		КонецЕсли;
		
		ЗначениеРеквизитаИзмененного = ДанныеОбъектаИзмененного[ИмяРеквизита];
		
		// Приводим тип оригинального реквизита к типу изменяемого реквизита
		Если НЕ ЗначениеЗаполнено(ЗначениеРеквизитаОригинального) Тогда
			Если ТипЗнч(ЗначениеРеквизитаИзмененного) = Тип("Булево") Тогда
				ЗначениеРеквизитаОригинального = Ложь;
			ИначеЕсли ТипЗнч(ЗначениеРеквизитаИзмененного) = Тип("Дата") Тогда
				ЗначениеРеквизитаОригинального = Дата(1, 1, 1);
			ИначеЕсли ТипЗнч(ЗначениеРеквизитаИзмененного) = Тип("Число") Тогда
				ЗначениеРеквизитаОригинального = 0;
			ИначеЕсли ТипЗнч(ЗначениеРеквизитаИзмененного) = Тип("Строка") Тогда
				ЗначениеРеквизитаОригинального = "";
			Иначе
				ЗначениеРеквизитаОригинального = Новый(ТипЗнч(ЗначениеРеквизитаИзмененного));
			КонецЕсли;
		КонецЕсли;

		СтруктураСопоставлениеРеквизитов = Новый Структура;
		СтруктураСопоставлениеРеквизитов.Вставить("Реквизит", ИмяРеквизита);
		СтруктураСопоставлениеРеквизитов.Вставить("Синоним", ПроверяемыйРеквизит.Значение);
		СтруктураСопоставлениеРеквизитов.Вставить("Значение", ЗначениеРеквизитаОригинального);
		СтруктураСопоставлениеРеквизитов.Вставить("НовоеЗначение", ЗначениеРеквизитаИзмененного);
		
		РезультатСравнения.СопоставлениеРеквизитов.Добавить(СтруктураСопоставлениеРеквизитов);
		
		Если ЗначениеРеквизитаОригинального = ЗначениеРеквизитаИзмененного Тогда
			Продолжить;
		КонецЕсли;
		
		РезультатСравнения.ЕстьИзменения = Истина;
		
		Если ЗначениеЗаполнено(ЗначениеРеквизитаИзмененного) Тогда
			РезультатСравнения.ЕстьИзмененияВЗаполненныхРеквизитах = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если РезультатСравнения.ЕстьИзменения Тогда
		ПараметрыФормы = Новый Структура(
				"СопоставлениеРеквизитов, РазрешитьОбновлять",
				РезультатСравнения.СопоставлениеРеквизитов, Истина);
		
		ФормаСравненияИзменений = ПолучитьФорму(
				"ОбщаяФорма.нсиФормаПодтвержденияИзмененияДанныхКонтрагентаПоЕГРЮЛ",
				ПараметрыФормы, ДанныеОбъектаИзмененного);
		
		ТекстЗаголовкаФормы = НСтр("ru = 'Отличие реквизитов заявки от реквизитов оригинала:'");
		
		ФормаСравненияИзменений.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		ФормаСравненияИзменений.Элементы.Декорация1.Заголовок = ТекстЗаголовкаФормы;
		ФормаСравненияИзменений.Элементы.Да.Видимость = Ложь;
		ФормаСравненияИзменений.Открыть();
	Иначе
		ТекстСообщения = НСтр("ru = 'Нет изменившихся реквизитов!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
КонецЕсли;

#КонецОбласти
// << 1БИТ, Скоков, 09.12.2020, Задача MDM-54

