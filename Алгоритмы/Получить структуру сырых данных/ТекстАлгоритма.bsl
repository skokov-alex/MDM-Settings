// >> 1БИТ, Скоков, 30.11.2020, Задача MDM-66
//
// Описание алгоритма:
// Вспомогательный алгоритм, используется в качестве параметра в алгоритме:
// "Сравнить с сырыми данными" (MDM-66)
//
// Настройки алгоритма:
// Наименование:			Получить структуру сырых данных
// Область применения:		Произвольный алгоритм
//
// Параметры передаваемые в алгоритм:
// ОбъектОбработки			БизнесПроцессСсылка.нсиЗаявкаНаИзменение
// РезультатВыполнения		Структура

#Область Алгоритм_получения_структуры_сырых_данных

ТекстЗапроса =
	"ВЫБРАТЬ
	|	нсиСформированныеПоСырымДаннымЗаявки.Заявка КАК Заявка,
	|	нсиСырыеДанные.Данные КАК Данные
	|ИЗ
	|	РегистрСведений.нсиСформированныеПоСырымДаннымЗаявки КАК нсиСформированныеПоСырымДаннымЗаявки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.нсиСырыеДанные КАК нсиСырыеДанные
	|		ПО нсиСформированныеПоСырымДаннымЗаявки.ИД = нсиСырыеДанные.ИД
	|ГДЕ
	|	нсиСформированныеПоСырымДаннымЗаявки.Заявка = &Заявка";

Запрос = Новый Запрос(ТекстЗапроса);
Запрос.УстановитьПараметр("Заявка", ОбъектОбработки);
Выборка = Запрос.Выполнить().Выбрать();

Если Выборка.Следующий() Тогда
	ДанныеXML = Выборка.Данные;
	Если ЗначениеЗаполнено(ДанныеXML) Тогда
		РезультатВыполнения = Новый Структура;
		
		ЧтениеXML = Новый ЧтениеXML;
		ЧтениеXML.УстановитьСтроку(ДанныеXML);
		xdtoОбъектОбработки = ФабрикаXDTO.ПрочитатьXML(ЧтениеXML);
		ОбъектОбработкиЗаявки = xdtoОбъектОбработки.Получить("Строка");
		
		Для Каждого СтрокаСвойств Из ОбъектОбработкиЗаявки.Свойства() Цикл
			РезультатВыполнения.Вставить(СтрокаСвойств.Имя, ОбъектОбработкиЗаявки.Получить(СтрокаСвойств));
		КонецЦикла;
	КонецЕсли;
КонецЕсли;

#КонецОбласти
// << 1БИТ, Скоков, 25.11.2020, Задача MDM-66

