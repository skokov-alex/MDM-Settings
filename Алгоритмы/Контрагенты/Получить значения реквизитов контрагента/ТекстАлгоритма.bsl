// >> 1БИТ, Скоков, 03.12.2020, Задача MDM-54
//
// Описание алгоритма:
// 		Вспомогательный алгоритм, используется в качестве параметра в алгоритме:
// 		"Показать измененные реквизиты контрагента" (MDM-54)
//
// Настройки алгоритма:
// 		Наименование			Получить значения реквизитов контрагента
// 		Область применения		Произвольный алгоритм
//
// Параметры передаваемые в алгоритм:
// 		ОбъектОбработки			СправочникСсылка.Контрагенты
// 		Параметры				Структура реквизитов контрагента
// 		РезультатВыполнения		Структура

#Область Алгоритм_получения_реквизитов_контрагента

СтруктураРеквизитов = Новый Структура;
СтруктураРеквизитов.Вставить("Код", "Код");
СтруктураРеквизитов.Вставить("Наименование", "Наименование");
СтруктураРеквизитов.Вставить("Родитель", "Родитель");
СтруктураРеквизитов.Вставить("ПометкаУдаления", "ПометкаУдаления");

МассивДополнительныхСвойств = Новый Массив;
МетаданныеКонтрагентаРеквизиты = ОбъектОбработки.Метаданные().Реквизиты;

Для Каждого Реквизит Из Параметры Цикл
	Если МетаданныеКонтрагентаРеквизиты.Найти(Реквизит.Ключ) = Неопределено Тогда
		МассивДополнительныхСвойств.Добавить(Реквизит.Ключ);
	Иначе
		СтруктураРеквизитов.Вставить(Реквизит.Ключ, Реквизит.Ключ);
	КонецЕсли;
КонецЦикла;

РезультатВыполнения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		ОбъектОбработки, СтруктураРеквизитов);

ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВременнаяТаблица.ИмяСвойства КАК ИмяСвойства,
	|	МАКСИМУМ(ВременнаяТаблица.Значение) КАК Значение
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДополнительныеРеквизитыИСведения.Имя КАК ИмяСвойства,
	|		нсиКонтрагентыДополнительныеРеквизиты.Значение КАК Значение
	|	ИЗ
	|		Справочник.нсиКонтрагенты.ДополнительныеРеквизиты КАК нсиКонтрагентыДополнительныеРеквизиты
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
	|			ПО нсиКонтрагентыДополнительныеРеквизиты.Свойство = ДополнительныеРеквизитыИСведения.Ссылка
	|	ГДЕ
	|		нсиКонтрагентыДополнительныеРеквизиты.Ссылка = &Ссылка
	|		И ДополнительныеРеквизитыИСведения.Имя В(&МассивДополнительныхСвойств)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДополнительныеРеквизитыИСведения.Имя,
	|		НЕОПРЕДЕЛЕНО
	|	ИЗ
	|		Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеРеквизиты КАК НаборыДополнительныхРеквизитовИСведений
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
	|			ПО НаборыДополнительныхРеквизитовИСведений.Свойство = ДополнительныеРеквизитыИСведения.Ссылка
	|	ГДЕ
	|		НаборыДополнительныхРеквизитовИСведений.Ссылка = ЗНАЧЕНИЕ(Справочник.НаборыДополнительныхРеквизитовИСведений.Справочник_нсиКонтрагенты)
	|		И ДополнительныеРеквизитыИСведения.Имя В(&МассивДополнительныхСвойств)) КАК ВременнаяТаблица
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблица.ИмяСвойства";

Запрос = Новый Запрос(ТекстЗапроса);
Запрос.УстановитьПараметр("МассивДополнительныхСвойств", МассивДополнительныхСвойств);
Запрос.УстановитьПараметр("Ссылка", ОбъектОбработки);

Выборка = Запрос.Выполнить().Выбрать();
Пока Выборка.Следующий() Цикл
	РезультатВыполнения.Вставить(Выборка.ИмяСвойства, Выборка.Значение);
КонецЦикла;

#КонецОбласти
// << 1БИТ, Скоков, 03.12.2020, Задача MDM-54

