// >> 1БИТ, Скоков, 11.12.2020, Задача MDM-50
// Ссылка на задачу:
// 		https://app.slack.com/client/T018U7C5KDX/G01EF5SEXNV/thread/G01EF5SEXNV-1606468424.064500
// 		Замечание № 32
//
// Описание алгоритма:
// 		Данный алгоритм является вспомогательным.
// 		Его необходимо добавить в следующие алгоритмы на закладку "Подчиненные алгоритмы":
// 		"Контрагенты Вид контрагента При изменении"
// 		"Контрагенты ИНН При изменении"
// 		"Контрагенты КПП При изменении"
// 		После добавления данного алгоритма на закладку "Подчиненные алгоритмы",
// 		В колонке "Объект обработки" необходимо указать значение Параметры
//
// Настройки алгоритма:
// 		Наименование - Алгоритм заполнения головного подразделения
// 		Область применения - Произвольный алгоритм
//
// Для работы алгоритма, необходимо на вкладке параметры добавить два параметра:
// 		1. Имя параметра: Подразделение, тип: Справочники.нсиУниверсальныйКлассификатор,
// 		Значение "Виды контрагентов" -> "Обособленное подразделение"
// 		2. Имя параметра: Филиал, тип Справочники.нсиУниверсальныйКлассификатор,
// 		Значение "Виды контрагентов" -> "Филиал"
//
// Параметры передаваемые в алгоритм:
// 		ОбъектОбработки.Объект - Структура формы объекта
// 		ОбъектОбработки.ИзменятьДанные - Булево - признак изменения данных
// 		ОбъектОбработки.Форма - ФормаКлиентскогоПриложения
// 		ОбъектОбработки.ЗаявкаОбъект - БизнесПроцессОбъект.нсиЗаявкаНаИзменение - объект бизнес-процесса

#Область Алгоритм_заполнения_головного_подразделения

Объект = ОбъектОбработки.Объект;
ВидКонтрагента = Объект.Вид_контрагента;
ГоловнойКонтрагент = Справочники.нсиКонтрагенты.ПустаяСсылка();

Если ВидКонтрагента = ПараметрыАлгоритма.Подразделение
	ИЛИ ВидКонтрагента = ПараметрыАлгоритма.Филиал Тогда
	
	ИНН = Объект.ИНН;
	КПП = Объект.КПП;
	
	Если ЗначениеЗаполнено(ИНН) И ЗначениеЗаполнено(КПП) Тогда
		МассивКПП56 = Новый Массив;
		МассивКПП56.Добавить("02");
		МассивКПП56.Добавить("03");
		МассивКПП56.Добавить("31");
		МассивКПП56.Добавить("32");
		МассивКПП56.Добавить("43");
		МассивКПП56.Добавить("45");
		
		ТекстЗапроса =
			"ВЫБРАТЬ
			|	нсиКонтрагенты.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.нсиКонтрагенты КАК нсиКонтрагенты
			|ГДЕ
			|	нсиКонтрагенты.ИНН = &ИНН
			|	И нсиКонтрагенты.КПП <> &КПП
			|	И НЕ ПОДСТРОКА(нсиКонтрагенты.КПП, 5, 2) В (&МассивКПП56)";
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("ИНН", ИНН);
		Запрос.УстановитьПараметр("КПП", КПП);
		Запрос.УстановитьПараметр("МассивКПП56", МассивКПП56);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Количество() = 1 Тогда
			Выборка.Следующий();
			ГоловнойКонтрагент = Выборка.Ссылка;
		Иначе
			// Если подходящих головных контрагентов несколько и 
			// один из них уже выбран, то мы его не удаляем
			ТекущийГоловнойКонтрагент = Объект.Головной_контрагент;
			Пока Выборка.Следующий() Цикл
				Если ТекущийГоловнойКонтрагент = Выборка.Ссылка Тогда
					ГоловнойКонтрагент = ТекущийГоловнойКонтрагент;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
КонецЕсли;

Если Объект.Головной_контрагент <> ГоловнойКонтрагент Тогда
	Объект.Головной_контрагент = ГоловнойКонтрагент;
	
	РеквизитИД = ОбъектОбработки.Форма.пМетаданные.Реквизиты.Головной_контрагент.Идентификатор;
	СтруктураОтбора = Новый Структура("РеквизитИД", РеквизитИД);
	НастройкиОбновленияДанных = ОбъектОбработки.ЗаявкаОбъект.НастройкиОбновленияДанных;
	СтрокаОбогащения = НастройкиОбновленияДанных.НайтиСтроки(СтруктураОтбора);
	
	Если СтрокаОбогащения.Количество() = 0 Тогда
		СтрокаОбогащения = НастройкиОбновленияДанных.Добавить();
		СтрокаОбогащения.РеквизитИД = РеквизитИД;
	Иначе
		СтрокаОбогащения = СтрокаОбогащения[0];
	КонецЕсли;
	
	СтрокаОбогащения.Обогащать = Истина;
	ОбъектОбработки.ИзменятьДанные = Истина;
КонецЕсли;

#КонецОбласти
// << 1БИТ, Скоков, 11.12.2020, Задача MDM-50

